# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

declare_args() {
  # DEPRECATED: The below variables are deprecated.
  # Instead use a config json as described in README.md.

  # set to true to bypass mi_dashboard launch.
  deprecated_no_mi_dashboard = false
}

source_set("user") {
  deps = [
    ":maxwell_main",
  ]
}

executable("maxwell_main") {
  output_name = "maxwell_main"

  sources = [
    "maxwell_main.cc",
  ]

  deps = [
    ":config",
    ":user_intelligence_provider",
    "//garnet/public/lib/app/cpp",
    "//peridot/public/lib/user_intelligence/fidl",
    "//garnet/public/lib/fsl",
    "//third_party/rapidjson",
  ]
}

source_set("config") {
  sources = [
    "config.cc",
    "config.h",
  ]
}

source_set("user_intelligence_provider") {
  sources = [
    "user_intelligence_provider_impl.cc",
    "user_intelligence_provider_impl.h",
  ]

  public_deps = [
    "//garnet/public/lib/app/cpp",
  ]

  deps = [
    ":agent_launcher",
    ":config",
    ":intelligence_services",
    "//garnet/public/lib/cobalt/fidl",
    "//peridot/public/lib/action_log/fidl",
    "//peridot/public/lib/context/fidl",
    "//peridot/public/lib/resolver/fidl",
    "//peridot/public/lib/suggestion/fidl",
    "//peridot/public/lib/suggestion/fidl:debug",
    "//peridot/public/lib/user_intelligence/fidl",
    "//peridot/public/lib/user_intelligence/fidl:scope",
    "//peridot/bin/acquirers/story_info:initializer",
    "//garnet/public/lib/bluetooth/fidl",
    "//garnet/public/lib/network/fidl",
  ]

  defines = []

  if (deprecated_no_mi_dashboard) {
    defines += [ "DEPRECATED_NO_MI_DASHBOARD" ]
  }
}

source_set("intelligence_services") {
  sources = [
    "intelligence_services_impl.cc",
    "intelligence_services_impl.h",
  ]

  deps = [
    "//peridot/public/lib/action_log/fidl",
    "//peridot/public/lib/context/fidl",
    "//peridot/public/lib/suggestion/fidl",
    "//peridot/public/lib/user_intelligence/fidl",
    "//peridot/public/lib/user_intelligence/fidl:scope",
  ]
}

source_set("agent_launcher") {
  sources = [
    "agent_launcher.cc",
    "agent_launcher.h",
  ]

  public_deps = [
    "//garnet/public/lib/app/cpp",
  ]

  deps = [
    "//peridot/lib/environment_host",
  ]
}

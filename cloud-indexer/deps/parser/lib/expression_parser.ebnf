(* Parser for the modular path expression language; EBNF notation according to
  <https://en.wikipedia.org/wiki/Extended_Backus%E2%80%93Naur_Form>. *)

(* Top level production. It's the pattern expression that appears in
   several yaml field values. Some field values such as verb and use
   match the label production. *)

pattern = path , [ structure ] ;

(* foo -> bar -> baz, _ -> foo -> bar -> baz *)
path = ( wildcard | property ) , { '->' , property } ;

(* foo { bar { baz }, bar2, bar3 { baz3 } } *)
structure = '{', property , [ structure ] , { property , [ structure ] } , '}' ;

(* foo? <bar> *)
property = label , [ cardinality ] , [ representation ] ;

(* foo? *)
cardinality = '*' | '?' | '+' | '!' ;

(* <foo, bar, baz> *)
representation = '<' , label-instance, { ',' , label-instance } , '>' ;

(* (foo bar baz) *)
label = label-instance | label-set ;
label-set = '(' , label-instance , { label-instance } , ')' ;

label-instance = url | shorthand;
url = ? anything non-whitespace that is parsed by Uri ? ;
shorthand = ? a name token that is declared in use ? ;

wildcard = '_' ;